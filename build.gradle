group 'com.epam'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'war'

sourceCompatibility = 1.8

buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }

    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

sourceSets {
    generated {
        java {
            srcDir 'src/main/generated'
        }
    }
}

configurations {
    querydslapt
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'com.querydsl:querydsl-apt:4.1.4'
    compile 'com.querydsl:querydsl-mongodb:4.1.4'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.5.3'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

task generateSources(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = ['-proc:only',
                            '-processor',
                            'org.springframework.data.mongodb.repository.support.MongoAnnotationProcessor']
    options.warnings = false
    destinationDir = file('src/main/generated')
    outputs.dir destinationDir
}

compileJava.source generateSources.outputs.files

clean {
    delete sourceSets.generated.java.srcDirs
}